package io.github.belgif.rest.guide.validator.rules.oas;

import io.github.belgif.rest.guide.validator.core.ViolationReport;
global io.github.belgif.rest.guide.validator.core.ViolationReport oas;
import org.eclipse.microprofile.openapi.models.media.Discriminator;
import io.github.belgif.rest.guide.validator.core.model.SchemaDefinition;
import io.github.belgif.rest.guide.validator.core.ViolationLevel;
import io.github.belgif.rest.guide.validator.core.ApiFunctions;
import io.github.belgif.rest.guide.validator.core.parser.Parser.ParserResult;
global io.github.belgif.rest.guide.validator.core.parser.Parser.ParserResult parserResult;
import java.util.List;
import java.util.Map;
import org.eclipse.microprofile.openapi.models.media.Schema;
import org.eclipse.microprofile.openapi.models.media.Schema.SchemaType;
import java.util.HashMap;
import java.util.Collections;
import java.util.ArrayList;
import java.util.Set;
import java.util.stream.Collectors
import java.util.HashSet;

function void violationDiscriminatorShouldContainMapping(ViolationReport oas, SchemaDefinition schema){
    oas.addViolation("[sch-allof]",
    "Discriminator does not have an explicit mapping of discriminator values to schemas.", null, schema, ViolationLevel.RECOMMENDED);
}

function void violationAllOfSubSchemasShouldNotDeclareTheSameProperties(ViolationReport oas, SchemaDefinition schema, String specificErrors) {
    oas.addViolation("[sch-allof]",
    "Subschemas in an allOf SHOULD NOT declare the same properties, because this will lead to problems for code generation tools.", specificErrors, schema, ViolationLevel.RECOMMENDED);
}

function String createViolationMessageForDoubleProperties(Map<String, List<String>> doubleProperties) {
    StringBuilder stringBuilder = new StringBuilder();
    for (Map.Entry<String, List<String>> doubleProperty : doubleProperties.entrySet()) {
      stringBuilder.append("Property <<").append(doubleProperty.getKey()).append(">> used in:\n");
      for (String pointer : doubleProperty.getValue()) {
        stringBuilder.append("\t").append(pointer).append("\n");
      }
    }
    return stringBuilder.toString();
}

function Map getDoubleProperties(Schema schema, ParserResult parserResult) {
  Map<String, List<String>> properties = new HashMap<>();
  for (Schema subSchema : schema.getAllOf()) {
    ApiFunctions.getRecursiveProperties(subSchema, parserResult).forEach((propertyName, propertyValue) -> {
      StringBuilder pointerBuilder = new StringBuilder();
      pointerBuilder.append(parserResult.resolve(subSchema).getPrintableJsonPointer());
      if (subSchema != propertyValue) {
        pointerBuilder.append(" (via <<").append(parserResult.resolve(propertyValue).getPrintableJsonPointer()).append(">>)");
      }
      if (properties.containsKey(propertyName)) {
        properties.get(propertyName).add(pointerBuilder.toString());
      } else {
        properties.put(propertyName, new ArrayList<>(Collections.singletonList(pointerBuilder.toString())));
      }
    });
  }
  return properties.entrySet().stream()
    .filter(e -> e.getValue().size() > 1)
    .collect(Collectors.toMap(Map.Entry::getKey, Map.Entry::getValue));
}

rule "SubSchemas in an allOf should not declare the same properties"
    when
        $schema: SchemaDefinition($model: /model, model.allOf != null)
        $errors: Map(!isEmpty()) from getDoubleProperties($model, parserResult)
    then
        violationAllOfSubSchemasShouldNotDeclareTheSameProperties(oas, $schema, createViolationMessageForDoubleProperties($errors));
end

rule "Discriminator Should Contain Explicit Mapping"
    when
        $schema: SchemaDefinition($discriminator: /model/discriminator)
        Discriminator(mapping == null || mapping.isEmpty()) from $discriminator
    then
        violationDiscriminatorShouldContainMapping(oas, $schema);
end